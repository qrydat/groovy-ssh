plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.4'
}

configure([compileJava, compileGroovy]) {
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6
    if (System.getenv('JAVA6_HOME')) {
        options.bootClasspath = "${System.getenv('JAVA6_HOME')}/jre/lib/rt.jar"
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'com.jcraft:jsch:0.1.53'
    compile 'com.jcraft:jsch.agentproxy.connector-factory:0.0.7'
    compile 'com.jcraft:jsch.agentproxy.jsch:0.0.7'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testRuntime 'cglib:cglib-nodep:3.2.1'
    testRuntime 'org.objenesis:objenesis:2.2'

    testRuntime 'ch.qos.logback:logback-classic:1.1.2'
}

processResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version])
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    from "${buildDir}/docs/groovydoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

description = 'Groovy SSH library'
ext {
    license = 'Apache-2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    vcsUrl = 'https://github.com/int128/groovy-ssh.git'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId parent.group
            artifactId parent.name
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name parent.name
                    description project.description
                    url project.vcsUrl
                    scm {
                        url project.vcsUrl
                        connection "scm:git:$project.vcsUrl"
                    }
                    licenses {
                        license {
                            name project.license
                            url project.licenseUrl
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id parent.group
                            name parent.group
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: findProperty('bintrayUser')
    key  = System.getenv('BINTRAY_KEY')  ?: findProperty('bintrayKey')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = parent.name
        licenses = [project.license]
        vcsUrl = project.vcsUrl
        version {
            name = project.version
        }
    }
}
